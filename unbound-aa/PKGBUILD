# Based on unbound PKGBUILD in arch-community trunk,
#   with less stuff built-in and a hacked-on authoritative-proxy mode
# UNBOUND_AA_NORA_MODE env var enables/disables the new mode
# Caching and authoritative mode should be normally be mutually-exclusive,
#   so do not use this hack unless you absolutely know what you're doing

pkgname=unbound-aa
_pkgname=unbound
conflicts=(unbound)

pkgver=1.13.1
pkgrel=2
pkgdesc="Hacked unbound version to add UNBOUND_AA_NORA_MODE env control/mode"
arch=(x86_64)
url="https://unbound.net/"
license=(BSD)
depends=(dnssec-anchors openssl ldns libevent libnghttp2)
makedepends=(expat systemd)
provides=(libunbound.so)

source=(
	"https://unbound.net/downloads/${_pkgname}-${pkgver}.tar.gz"
	add-aa-nora-mode-hack.patch
	${_pkgname}-sysusers.conf
	${_pkgname}-tmpfiles.conf
	${_pkgname}-trusted-key.hook )

b2sums=(
	5fabb9205773a1983842e41cf7a4d6c3878fa8beb7c8ccc71ae1edf7738cb9506c3d7bb32cf887b305317ca695bf876d9f5bf9aeb0129b0e9e926d437b3e6eb3
	SKIP
	292a3c2e5fde292a03b6c9b2ddabd5089f52e73b50a404c3d9f54c1a43184924b661a21eea61cc521c594c1005a3b40b630fa585a38195c61298f9b24b248b92
	d3951006b43068be904c6b91a9e0563d56228225854e12b40abbdd4ba9b47338e97265837297a6de879acbc8051bb749163f9457683f5e12fc29ac2e7b687fd3
	d28785390eb6c125bd26ca11f097fe8864b080482157deeb7c70e9bee47ff2844abaed574db59a7c152ed3ec0acba05cfee4c3751f7a9f553320b064578f86c7 )

prepare() {
	cd ${_pkgname}-${pkgver}
	# set default location of trusted-key.key
	sed -i -e '/# trust-anchor-file:/c\\ttrust-anchor-file: /etc/unbound/trusted-key.key' doc/example.conf.in

	p=add-aa-nora-mode-hack.patch; echo "--- patch: $p"
	patch --dry-run -tNp1 -l -i "${srcdir}"/$p && patch -tNp1 -l -i "${srcdir}"/$p || return 1

	autoreconf -vfi
}

build() {
	cd ${_pkgname}-${pkgver}
	local opts=(
		--prefix=/usr
		--sysconfdir=/etc
		--localstatedir=/var
		--sbindir=/usr/bin
		--disable-rpath
		--disable-dnscrypt
		--disable-dnstap
		--enable-pie
		--enable-relro-now
		--enable-subnet
		--enable-systemd
		--enable-tfo-client
		--enable-tfo-server
		--enable-cachedb
		--without-libhiredis
		--with-conf-file=/etc/unbound/unbound.conf
		--with-pidfile=/run/unbound.pid
		--with-libevent
		--with-libnghttp2
		--without-pyunbound
	)
	./configure "${opts[@]}"
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd ${_pkgname}-${pkgver}
	make -k check
}

package() {
	depends+=(libsystemd.so)
	cd ${_pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -vDm 644 contrib/${_pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
	install -vDm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${_pkgname}/
	cd ..
	install -vDm 644 ${_pkgname}-sysusers.conf "${pkgdir}"/usr/lib/sysusers.d/${_pkgname}.conf
	install -vDm 644 ${_pkgname}-tmpfiles.conf "${pkgdir}"/usr/lib/tmpfiles.d/${_pkgname}.conf
	# libalpm hook to copy the dnssec-anchors provided key to /etc/unbound
	install -vDm 644 unbound-trusted-key.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}
