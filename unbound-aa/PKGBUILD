# Based on unbound PKGBUILD in arch-community trunk,
#   with less stuff built-in and an extra authoritative-proxy binary.

pkgname=unbound-aa
_pkgname=unbound
conflicts=(unbound)

pkgver=1.13.1
pkgrel=2
pkgdesc="Validating, recursive, and caching DNS resolver"
arch=(x86_64)
url="https://unbound.net/"
license=(BSD)
depends=(openssl ldns libevent libnghttp2)
makedepends=(expat systemd)
provides=(libunbound.so)

source=(
	"https://unbound.net/downloads/${_pkgname}-${pkgver}.tar.gz"
	set-aa-bit-from-dnstap_send_identity.patch
	${_pkgname}-sysusers.conf
	${_pkgname}-tmpfiles.conf )

b2sums=(
	5fabb9205773a1983842e41cf7a4d6c3878fa8beb7c8ccc71ae1edf7738cb9506c3d7bb32cf887b305317ca695bf876d9f5bf9aeb0129b0e9e926d437b3e6eb3
	SKIP
	292a3c2e5fde292a03b6c9b2ddabd5089f52e73b50a404c3d9f54c1a43184924b661a21eea61cc521c594c1005a3b40b630fa585a38195c61298f9b24b248b92
	d3951006b43068be904c6b91a9e0563d56228225854e12b40abbdd4ba9b47338e97265837297a6de879acbc8051bb749163f9457683f5e12fc29ac2e7b687fd3 )

prepare() {
	cd ${_pkgname}-${pkgver}

	p=set-aa-bit-from-dnstap_send_identity.patch; echo "--- patch: $p"
	patch --dry-run -tNp1 -i "${srcdir}"/$p && patch -tNp1 -i "${srcdir}"/$p

	autoreconf -vfi
}

build() {
	cd ${_pkgname}-${pkgver}
	local opts=(
		--prefix=/usr
		--sysconfdir=/etc
		--localstatedir=/var
		--sbindir=/usr/bin
		--disable-rpath
		--disable-dnscrypt
		--disable-dnstap
		--enable-pie
		--enable-relro-now
		--enable-subnet
		--enable-systemd
		--enable-tfo-client
		--enable-tfo-server
		--enable-cachedb
		--without-libhiredis
		--with-conf-file=/etc/unbound/unbound.conf
		--with-pidfile=/run/unbound.pid
		--with-libevent
		--with-libnghttp2
		--without-pyunbound
	)
	./configure "${opts[@]}"
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd ${_pkgname}-${pkgver}
	make -k check
}

package() {
	depends+=(libprotobuf-c.so libsystemd.so)
	cd ${_pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -vDm 644 contrib/${_pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
	install -vDm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${_pkgname}/
	cd ..
	install -vDm 644 ${_pkgname}-sysusers.conf "${pkgdir}"/usr/lib/sysusers.d/${_pkgname}.conf
	install -vDm 644 ${_pkgname}-tmpfiles.conf "${pkgdir}"/usr/lib/tmpfiles.d/${_pkgname}.conf
}
