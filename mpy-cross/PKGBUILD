pkgname=mpy-cross
pkgver=1.23.0
pkgrel=1
pkgdesc="MicroPython cross compiler for .py scripts into .mpy files"
arch=(any)
url=https://docs.micropython.org/en/latest/reference/mpyfiles.html
license=(MIT)
depends=()
makedepends=(python3)
source=(https://github.com/micropython/micropython/releases/download/v${pkgver}/micropython-${pkgver}.tar.xz)
b2sums=(3fca138bbe9a676c671812625ac374e82c3c91bb41db32557747ea4d28941de92be80bbf2e642f9094427dbcf6ee285c63ddad66510287074edb716017599384)

prepare() {
	cd micropython-${pkgver}/mpy-cross
	sed -i 's:CWARN = -Wall -Werror:CWARN =:' Makefile
}

build() {
	cd micropython-${pkgver}/mpy-cross
	make
}

package() {
	cd micropython-${pkgver}/mpy-cross
	install -Dm0755 build/mpy-cross ${pkgdir}/usr/bin/mpy-cross
}
