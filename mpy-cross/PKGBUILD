pkgname=mpy-cross
pkgver=1.24.1
pkgrel=1
pkgdesc="MicroPython cross compiler for .py scripts into .mpy files"
arch=(any)
url=https://docs.micropython.org/en/latest/reference/mpyfiles.html
license=(MIT)
depends=()
makedepends=(python3)
source=(https://github.com/micropython/micropython/releases/download/v${pkgver}/micropython-${pkgver}.tar.xz)
b2sums=(cc53bfbc9f42007dccc3d6344c17a7e990dae8464be69e1f16be0c5cc939d5b2829d1eab414432be4fca8f95843e433e2c1f25700ca50fbbe2eeb9ab4be56fb8)

prepare() {
	cd micropython-${pkgver}/mpy-cross
	sed -i 's:CWARN = -Wall -Werror:CWARN =:' Makefile
}

build() {
	cd micropython-${pkgver}/mpy-cross
	make
}

package() {
	cd micropython-${pkgver}/mpy-cross
	install -Dm0755 build/mpy-cross ${pkgdir}/usr/bin/mpy-cross
}
