# Based on https://aur.archlinux.org/packages/waterfox-current-git/ by Matt/ilikenwf <parwok@gmail.com>
# Last sync with upstream git - 8f99d55 2019-11-30 pkgver=68.0+04cd81779251

# Local changes:
# - Builds specific waterfox-current git release tag from https://github.com/MrAlex94/Waterfox/releases
# - Downloads release .tar.gz instead of cloning whole history, should still be ~500M compressed though
# - Use bundled spelling dictionaries instead of depending on hunspell
# - Bunch of local patches for various minor tweaks - hotkeys, hacks, etc

# Local pkgver: 2020.01-current -> 2020.01
# Local pkgver: 2019.10-current-1 -> 2019.10.1

# Missing build-deps here:
# imake inetutils xorg-server-xvfb autoconf2.13 llvm python2-psutil cbindgen nasm rust

pkgver=2020.06
pkgver_tag=${pkgver}-current
# pkgver_tag=${pkgver%.*}-current-${pkgver##*.}

pkgname=waterfox-current
pkgrel=1
pkgdesc="More private and optimized Firefox fork"
arch=(i686 x86_64)
license=(MPL)
url="https://www.waterfoxproject.org/"

depends=(
	gtk3 libffi libvpx icu libevent mozilla-common libxt
	startup-notification mime-types dbus-glib ffmpeg nss ttf-font libpulse )

makedepends=(
	unzip zip diffutils python2-setuptools yasm mesa imake inetutils gtk2
	xorg-server-xvfb autoconf2.13 rust mercurial clang llvm jack
	python nodejs python2-psutil cbindgen nasm )

# gtk2 - https://bugzilla.mozilla.org/show_bug.cgi?id=1377445
# python2 - https://bugzilla.mozilla.org/show_bug.cgi?id=1093212

optdepends=(
	'libnotify: Notification integration'
	'pulseaudio: Audio support'
	'speech-dispatcher: Text-to-Speech' )

source=(
	https://github.com/MrAlex94/Waterfox/archive/"$pkgver_tag".tar.gz
	mozconfig
	waterfox-current.desktop
	ignore_bad_validator.patch

	# Local patches
	enable_reader_mode_hotkey.patch
	change_quit_hotkey_to_ctrl_shift_alt_q.patch
	resist_fp_exclude_dark_mode.patch
)

sha512sums=(
	ceec00ac535d2bb529da82022b4d1006abde647cf774846c2b3ee56d1bfc8e834a7dcb7e2374ee82caedb0dc6c0aeccc6047ff9cca8ba7966715e8f760566f6e
	7c130bc06f828929af82fb038e2870acec408a82809dbc2024859417a2aff3b4fcc5d9a99f17e0f0161def8a5405460ff76da0662acf2d4f9ab77199609d8d3a
	acd1a8ea32747dcd3df976c64408bb01d06f30af399f8625f43930d6a8b64a76a00af08aca9cd525c74ee3f2e58f6a49b57bab9a8b6ec0e1497f27122a41b82a
	ab2aced2e371afad317ab3ffb3e8161c457f022327e182d426aa2ba4142112060225ced4610eb2525e1c739a4e56ad4e7cf78cc102232cf01cf06d0224a9c09d

	# Local patches
	8fe402c76c125c6b53f2c3aeeb0e78c4843b12f0d86f105d0db6f820d6ebf99f884fad56110a14b81d9bab8e237d51d43633885f8000d283631c4a2e0b1e08af
	222347a0aa35768ed67d393861fd733e914f8546ace85ef6f8e26175703c3e256261d8421b66d86e65f887b6530ab58fe02a49340a7abbad6c257845ec7ffdd0
	d6b0d1d7d761bd711142f1e978f5b7ae46bd99745f6f6f21fa8a1d95cf506492c03b83d63f25d5e8f5aacd4c5cc3f12b58512f726fede5320839d2dda9ec1846
)

options=(!emptydirs !makeflags)
provides=()
conflicts=()
install=waterfox-current.install
options=(!emptydirs !makeflags !strip)


prepare() {
	rm -rf Waterfox # old version
	mv Waterfox-$pkgver_tag Waterfox

	cd Waterfox

	mv .mozconfig{,.default}
	cp ../mozconfig .mozconfig

	# lcrmf breaks stuff
	sed -i 's/ \-lcrmf//g' "${srcdir}/Waterfox/old-configure.in"

	# the below is a really horrible thing to do but it allows commas in our optimizations to work
	patch -tNp1 -i "${srcdir}/ignore_bad_validator.patch"

	# Enable Alt+R hotkey for reader mode
	patch -tNp1 -i "${srcdir}/enable_reader_mode_hotkey.patch"
	# Use Ctrl+Shift+Alt+Q for quit, to avoid accidentally pressing it instead of Ctrl+W
	patch -tNp1 -i "${srcdir}/change_quit_hotkey_to_ctrl_shift_alt_q.patch"
	# Pass "prefers-color-scheme: dark" css media queries in resistFingerprinting mode too
	patch -tNp1 -i "${srcdir}/resist_fp_exclude_dark_mode.patch"

	mkdir -p "$srcdir/path"
}

build() {
	cd Waterfox

	export PATH="$srcdir/path:$PATH"
	export PYTHON="/usr/bin/python2"
	export MOZ_NOSPAM=1
	export MOZBUILD_STATE_PATH="$srcdir/mozbuild"

	# LTO needs more open files
	ulimit -n 4096

	xvfb-run -a -n 97 -s "-screen 0 1600x1200x24" ./mach build
}

package() {
	cd Waterfox
	mkdir -p "$pkgdir"
	DESTDIR="$pkgdir" ./mach install

	for i in 16 32 64 48 128; do
		install -Dm644 \
			"$srcdir/Waterfox/obj-x86_64-pc-linux-gnu/dist/waterfox-current/browser/chrome/icons/default/default$i.png" \
			"$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/waterfox-current.png"
	done

	install -Dm644 \
		"$srcdir/Waterfox/obj-x86_64-pc-linux-gnu/dist/waterfox-current/browser/chrome/icons/default/default128.png" \
		"$pkgdir/usr/share/icons/hicolor/64x64/apps/waterfox-current.png"
	install -Dm644 \
		"$srcdir/Waterfox/obj-x86_64-pc-linux-gnu/dist/waterfox-current/browser/chrome/icons/default/default128.png" \
		"$pkgdir/usr/share/icons/hicolor/128x128/apps/waterfox-current.png"
	install -Dm644 browser/branding/official/content/about-logo.png \
		"$pkgdir/usr/share/icons/hicolor/192x192/apps/waterfox-current.png"
	install -Dm644 browser/branding/official/content/about-logo@2x.png \
		"$pkgdir/usr/share/icons/hicolor/384x384/apps/waterfox-current.png"

	install -Dm644 ../waterfox-current.desktop \
		"$pkgdir/usr/share/applications/waterfox-current.desktop"
}
