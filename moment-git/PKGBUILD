_p=moment

pkgname=${_p}-git
pkgver=1
pkgrel=2
pkgdesc='A Matrix client; forked from Mirage'
arch=(x86_64)
url='https://mx-moment.xyz/'
license=(LGPL3)
depends=(
	qt5-base qt5-declarative qt5-quickcontrols2
	qt5-svg qt5-graphicaleffects qt5-imageformats
	libx11 libxss alsa-lib )
makedepends=(cmake git)
source=("${_p}::git+https://gitlab.com/mx-moment/moment.git")
sha256sums=(SKIP)

pkgver() {
	cd $_p
	local tag=$(git tag --sort=-v:refname | head -1)
	local commits_since=$(git rev-list $tag..HEAD --count)
	echo "$tag.r$commits_since.$(git log --pretty=format:'%h' -n 1)"
}

prepare() {
	cd $_p
	git submodule update --init --recursive
}

build() {
	cd $_p
	export QT_SELECT=5
	make clean ||:
	qmake PREFIX=/usr moment.pro
	make
}

package() {
	cd $_p
	make INSTALL_ROOT="${pkgdir}" install

	req_file=/usr/share/doc/$_p/requirements.txt
	install -Dvm644 requirements.txt "$pkgdir/$req_file"
	echo >&2 "NOTE: This build REQIRES installing python dependency packages manually"
	echo >&2 "NOTE: Use 'python -m venv ...' or just pip and $req_file directly for that"
}
