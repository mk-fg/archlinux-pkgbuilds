# Upstream PKGBUILD from gitlab.com/virtio-fs/qemu sources with virtio-fs support
# Also slimmed-down on targets and various stuff like spice, ceph, docs, etc - for faster build and less deps
# More info on virtio-fs: https://virtio-fs.gitlab.io/howto-qemu.html

# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: SÃ©bastien "Seblu" Luttringer <seblu@seblu.net>

pkgname=qemu
pkgdesc="A generic and open source machine emulator and virtualizer"

pkgver=4.2.0
pkgver_virtiofs=virtio-fs-dev
# pkgver_virtiofs=virtio-fs-as-posted-2020-01-21

pkgrel=2
arch=(x86_64)
license=(GPL2 LGPL2.1)
url="https://wiki.qemu.org/"
depends=(virglrenderer sdl2 vte3 libpulse
	gnutls libpng libaio numactl jemalloc xfsprogs libnfs
	lzo snappy curl vde2 libcap-ng libcacard usbredir libslirp libssh)
makedepends=(python)

optdepends=('samba: SMB/CIFS server support')
provides=(qemu-headless)
conflicts=(qemu-headless)
replaces=(qemu-kvm)

source=(
	65-kvm.rules)
sha512sums=(
	bdf05f99407491e27a03aaf845b7cc8acfa2e0e59968236f10ffc905e5e3d5e8569df496fd71c887da2b5b8d1902494520c7da2d3a8258f7fd93a881dd610c99)

case $CARCH in
	i?86) _corearch=i386 ;;
	x86_64) _corearch=x86_64 ;;
esac

prepare() {
	[[ -e "${pkgname}-${pkgver_virtiofs}" ]] \
		|| git clone --depth=1 --single-branch -b "$pkgver_virtiofs" \
			https://gitlab.com/virtio-fs/qemu.git "${pkgname}-${pkgver_virtiofs}"
	mkdir -p build
}

build() (
	cd build

	# qemu vs. make 4 == bad
	export ARFLAGS=rv

	# http://permalink.gmane.org/gmane.comp.emulators.qemu/238740
	export CFLAGS+=" -fPIC"

	../${pkgname}-${pkgver_virtiofs}/configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/qemu \
		--smbd=/usr/bin/smbd \
		--enable-modules \
		--enable-sdl \
		--enable-jemalloc \
		--enable-slirp=system \
		\
		--audio-drv-list="pa alsa sdl" \
		--target-list=x86_64-softmmu \
		--disable-docs \
		--disable-spice \
		--disable-brlapi \
		--disable-rbd \
		--disable-glusterfs \
		--disable-libiscsi \
		\
		"${@:2}"

	make
	make virtiofsd
)

package() {
	optdepends+=()
	install=qemu.install
	options=(!strip)

	make -C build DESTDIR="$pkgdir" install "${@:2}"

	# systemd stuff
	install -Dm644 65-kvm.rules "$pkgdir/usr/lib/udev/rules.d/65-kvm.rules"

	# remove conflicting /var/run directory
	cd "$pkgdir"
	rm -r var

	cd usr/lib
	tidy_strip

	cd ../bin
	tidy_strip
}
