_p=libreswan

pkgname=${_p}-git
pkgver=3.28.20627.2480166f7
pkgrel=1
pkgdesc="IPsec implementation for Linux"
arch=(i686 x86_64)
url='https://libreswan.org/'
license=(GPL BSD MPL)
depends=(systemd nss libcap-ng libevent libseccomp)
makedepends=(git docbook-xsl xmlto flex bison)
conflicts=(libreswan freeswan openswan strongswan ipsec-tools)
backup=(etc/ipsec.conf etc/ipsec.secrets)
source=("${_p}::git+https://github.com/libreswan/libreswan/")
sha256sums=(SKIP)


libreswan_opts=(
	INITSYSTEM=systemd
	USE_DNSSEC=false
	USE_SYSTEMD_WATCHDOG=true
	USE_KEYRR=false
	USE_KLIPS=false
	USE_XAUTHPAM=false
	USE_LIBCAP_NG=true
	USE_NM=false
	USE_LIBCURL=false
	USE_EXTRACRYPTO=false
	USE_SECCOMP=true
	WERROR_CFLAGS=
	USE_LABELED_IPSEC=false
	USE_LINUX_AUDIT=false

	INC_USRLOCAL=/usr INC_MANDIR=share/man
	FINALSBINDIR=/usr/bin FINALLIBEXECDIR=/usr/lib/ipsec
)


pkgver() {
	cd $_p
	rel=$(awk '/^v[0-9]+\.[0-9]+\>/ {sub(/^v/,"",$1); print $1; exit}' CHANGES)
	[[ -n "$rel" ]] || exit 1
	printf "%s.%s.%s" "$rel" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd $_p
	make "${libreswan_opts[@]}" programs
}

package() {
	cd $_p
	make "${libreswan_opts[@]}" DESTDIR="$pkgdir/" install

	sed -i '1s|python|python2|' "$pkgdir"/usr/lib/ipsec/verify
	mkdir -p "${pkgdir}"/usr/lib/tmpfiles.d
	cat >"${pkgdir}"/usr/lib/tmpfiles.d/libreswan.conf <<EOF
d /run/pluto 0755 - - -
EOF
	rm -rf "${pkgdir}"/{var,etc/pam.d}
}
