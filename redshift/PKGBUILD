# Copied from Arch community/redshift trunk on 2019-11-20
# Disabled geoclue2/gui and bump MAX_BRIGHTNESS to allow soft-brightness adjustment beyond 1.0

# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Geoffrey Teale <tealeg@stop-squark>
# Contributor: Mark, Huo Mian <markhuomian[at]gmail[dot]com>
# Contributor: Biginoz <biginoz a free dot fr>
# Contributor: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=redshift
pkgver=1.12
pkgrel=3
pkgdesc='Adjusts the color temperature of your screen according to your surroundings.'
arch=(x86_64)
url=http://jonls.dk/redshift/
license=(GPL3)
depends=(libdrm libxcb libxxf86vm)
makedepends=(intltool python)
source=("https://github.com/jonls/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=(5d04f2413dacdf3434cb86f373842462)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	OPTS=(
		--prefix=/usr
		--with-systemduserunitdir=/usr/lib/systemd/user
		--enable-drm --enable-randr --enable-vidmode
		--disable-geoclue2 --disable-gui
	)

	sed -i 's: MAX_BRIGHTNESS \+1\.0$: MAX_BRIGHTNESS 3.0:' src/redshift.c
	./configure "${OPTS[@]}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
