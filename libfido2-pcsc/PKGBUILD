pkgname=libfido2-pcsc
pkgver=1.12.0
pkgrel=1
pkgdesc='Library functionality for FIDO 2.0, with env-var-enabled support for PCSC devices'
url='https://developers.yubico.com/libfido2/'
arch=(x86_64)
license=(BSD)
depends=(glibc openssl libcbor libcbor.so hidapi systemd-libs  libudev.so zlib pcsclite)
makedepends=(cmake systemd)
provides=(libfido2.so libfido2)
conflicts=(libfido2)
source=( "https://developers.yubico.com/libfido2/Releases/libfido2-${pkgver}.tar.gz" )
sha256sums=( 813d6d25116143d16d2e96791718a74825da16b774a8d093d96f06ae1730d9c5 )

_patches=( 0001_enable-pcsc-via-env-var.patch )
for p in ${_patches[@]}; do source+=($p); sha256sums+=(SKIP); done

prepare() {
  cd libfido2-${pkgver}
	for p in ${_patches[@]}
	do patch --dry-run -tNp1 -i "$srcdir"/$p >/dev/null && patch -tNp1 -i "$srcdir"/$p
	done
}

build() {
  cd libfido2-${pkgver}
  # do not use bundled udev rules superseded by systemd
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -Wno-dev \
    -DUSE_PCSC=1 \
    -DNFC_LINUX=1 \
    -S .
  make -C build VERBOSE=1
}

package() {
  cd libfido2-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README.adoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
