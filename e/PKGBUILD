# Based on upstream pkgbuild from SVN, with a few local patches and less deps
# Renamed to "e" for simplicitly and to not need IgnorePkg= and such

pkgname=e
_p=enlightenment

pkgver=0.23.1
pkgrel=2
pkgdesc="Enlightenment window manager"
arch=(x86_64)
url="http://www.enlightenment.org"
license=(BSD)
conflicts=($_p)
depends=(
	efl xcb-util-keysyms hicolor-icon-theme pixman mesa
	desktop-file-utils udisks2 ttf-font bluez-libs pam )
optdepends=(
	'connman: network module'
	'acpid: power events on laptop lid close'
	'geoip-database: geolocation module'
	'packagekit: needed for packagekit module' )
makedepends=(meson ninja)
provides=(notification-daemon)
backup=(
	etc/enlightenment/sysactions.conf
	etc/xdg/menus/e-applications.menu )
source=( "https://download.enlightenment.org/rel/apps/$_p/$_p-$pkgver.tar.xz" )
sha256sums=( e530590c09b560679621f4531d55c242cfafe8523309e0ae88fb267e00f52c34 )

build() {
	cd "$srcdir/$_p-$pkgver"
	CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
	CFLAGS+=' -fvisibility=hidden'
	mkdir -p build
	meson --prefix=/usr . build
	ninja -C build
}

package() {
	cd "$srcdir/$_p-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
}
