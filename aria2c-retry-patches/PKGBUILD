## Based on upstream ArchLinux PKGBUILD from community repo
##   with patches from https://github.com/P3TERX/Aria2-Pro-Core
## Main intention is to have it retry connection on options like lowest-speed-limit instead of failing,
##   to work around issues with known-throttled services, where this is expected, like yt with youtube-dl.

_p=aria2
pkgname=${_p}-retry-patches
pkgver=1.36.0
pkgrel=2
pkgdesc="Lightweight multi-protocol & multi-source, cross platform download cli utility, with extra retry-on-X patches."
arch=(x86_64)
url=https://github.com/aria2/aria2
license=(GPL)
depends=(gnutls libxml2 sqlite c-ares ca-certificates libssh2)
makedepends=()
conflicts=(aria2)

source=(
	"https://github.com/tatsuhiro-t/aria2/releases/download/release-$pkgver/aria2-$pkgver.tar.xz"
	0002-download-retry-on-slow-speed-and-reset.patch
	0003-option-add-option-to-retry-on-http-4xx.patch
	redirect-logging-info-level.patch
)

sha512sums=(
	8203dbb75274455a78c50dd4f894e631de6931ac889f26896dceed78ec38c98cdbcf07e164744f308f2bfffeae1016beec1bfdbe8cad7f3280d11376aa0c2542
	6ff654c2774376141991a07f5e23a152376243b4d336e0e3d737bf83bd541f20012f3af7d2f530d43167e8b76655482f1d08f646b29eba30fb09fc3c3bc7f5c3
	db77254b28059f305d4d6168be1c37ee2ee5a8f8aaf2a49ca514a5652414eb7f492d2ee501e39d1d3f735de6a3ae5902200cbdef03664b312fccc2fdee08f6a5
	568222bac857502a7fac23cc47eec3e842a673273982c2274d8a9a5ee854739d6377e3f67561d1096187d6e6a664dc9590d53a1b2655ac7e22ce6657fc16bd7d
)

prepare() {
	cd $_p-$pkgver
	for p in \
		0002-download-retry-on-slow-speed-and-reset.patch \
		0003-option-add-option-to-retry-on-http-4xx.patch \
		redirect-logging-info-level.patch
	do echo "--- patch: $p"; patch --dry-run -tNp1 -l -i "${srcdir}"/$p && patch -tNp1 -l -i "${srcdir}"/$p || return 1; done
}

build() {
	cd $_p-$pkgver
	./configure \
		--prefix=/usr \
		--enable-libaria2 \
		--with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
	make
}

package() {
	cd $_p-$pkgver
	make DESTDIR="$pkgdir" install
}
